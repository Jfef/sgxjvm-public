// Put the Dev Preview credentials here
def username = null
def password = null

// The credentials may be specified in the environment as well
def mavenUsername = username ?: System.getenv('OBLIVIUM_MAVEN_USERNAME')
def mavenPassword = password ?: System.getenv('OBLIVIUM_MAVEN_PASSWORD')
def dockerUsername = username ?: System.getenv('OBLIVIUM_CONTAINER_REGISTRY_USERNAME')
def dockerPassword = password ?: System.getenv('OBLIVIUM_CONTAINER_REGISTRY_PASSWORD')

if ([mavenUsername, mavenPassword, dockerUsername, dockerPassword].any { element -> element == null }) {
    throw new InvalidUserDataException(
            'Please specify the Dev Preview credentials in credentials.gradle. See https://www.corda.net/develop/sgxjvm.html for details.'
    )
}

repositories {
    maven {
        url = "https://software.r3.com/artifactory/sgxjvm"
        credentials { creds ->
            creds.username = mavenUsername
            creds.password = mavenPassword
        }
    }
}

if (this.hasProperty('ext')) {
    ext.mavenUsername = mavenUsername
    ext.mavenPassword = mavenPassword
    ext.dockerUsername = dockerUsername
    ext.dockerPassword = dockerPassword
}
